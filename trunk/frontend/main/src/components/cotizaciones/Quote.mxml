<?xml version="1.0" encoding="ISO-8859-1"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:components="components.*"
		 creationComplete="creationCompleteHandler(event)"
		 >
	<mx:Script>
		<![CDATA[
			import model.ArchivoCotizacion;
			import model.Constants;
			import model.Cotizacion;
			import model.Util;
			
			import mx.collections.ArrayCollection;
			import mx.com.gahm.componentes.validadores.InicializaValidadoresEtiquetas;
			import mx.com.gahm.componentes.validadores.ValidadorCadenaCualquierCaracter;
			import mx.com.gahm.componentes.validadores.ValidadorDecimal;
			import mx.com.gahm.componentes.validadores.ValidadorNumerico;
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			import mx.utils.object_proxy;
			
			private var mode:String = null;
			private var _item:Cotizacion;
			private var validators:InicializaValidadoresEtiquetas;
			
			public function set registrer(value:Boolean):void{
				mode = Constants.REGISTER_MODE;
				this.label = Constants.REGISTRAR;
			}
			
			public function set update(value:Boolean):void{
				mode = Constants.UPDATE_MODE;
				this.label = Constants.MODIFICAR;
			}
			
			public function set enabledComponents(value:Boolean):void{
			}
			
			protected function creationCompleteHandler(event:FlexEvent):void
			{
				this.modeValidation();
				this.initializeValidators();
				this.fillCombos();
			}
			
			public function set quote(value:Cotizacion):void{
				if( value == null ){
					return;
				}
				this.cleanForm();
				this._item = value;
				this.setQuote(); 
			}
			
			private function setQuote():void
			{
				if( this._item == null ){
					return;
				}
				this.txtID.text = String(this._item.id);
				this.txtDescription.text = this._item.descripcion;
				this.txtTransmitter.text = this._item.emisor;
				this.dtfDate.selectedDate = this._item.fecha;
				this.dlvFileName.delivers = getEntregables( this._item.archivosCotizacion );
				this.comments.setCommentsIn( this._item.comentariosCotizacion, "comentarioCompras");
			}
			
			public static function getEntregables(entregables:ArrayCollection):ArrayCollection{
				if( entregables == null || entregables.length == 0 ){
					return null;
				}
				var filterEntregables:ArrayCollection = new ArrayCollection();
				for each (var entregable:ArchivoCotizacion in entregables) 
				{
					if( !(entregable == null) ){
						filterEntregables.addItem( entregable.nombreArchivo );
					}
				}
				return filterEntregables;
			}
			
			private function getQuote():Cotizacion{
				var item:Cotizacion = null;
				if( this.mode == Constants.REGISTER_MODE ){
					item = new Cotizacion();
				}else{
					item = this._item;
				}
				item.descripcion = Util.validateEmpty( this.txtDescription.text );
				item.emisor = Util.validateEmpty( this.txtTransmitter.text );
				item.fecha = this.dtfDate.selectedDate;
				if( !(this.dlvFileName.delivers == null) &&
					(this.dlvFileName.delivers.length > 0) ){
					item.archivosCotizacion = setEntregables( this.dlvFileName.delivers );
				}
				item.comentariosCotizacion = this.comments.getCommentsAsInWith("model.ComentarioCompras","model.ComentarioCotizacion","comentarioCompras")
				return item;
			}
			
			public static function setEntregables( filesName:ArrayCollection ):ArrayCollection{
				if( filesName == null || 
					filesName.length == 0 ){
					return null;
				}
				var archivoCotizacion:ArchivoCotizacion = null;
				var archivosCotizacion:ArrayCollection = new ArrayCollection();
				for each (var fileName:String in filesName) 
				{
					archivoCotizacion = new ArchivoCotizacion();
					archivoCotizacion.nombreArchivo = fileName;
					archivosCotizacion.addItem( archivoCotizacion );
				}
				return archivosCotizacion;
			}
			
			private function fillCombos():void
			{
			}
			
			protected function cotizacionService_faultHandler(event:FaultEvent):void
			{
				Util.showErrorMessage( Util.splitException( event.fault.faultString ) );
			}
			
			protected function btnAccept_clickHandler(event:MouseEvent):void
			{
				if( !this.validations() ){
					Util.showMessageVerifyFields();
					return;
				}
				if( this.mode == Constants.REGISTER_MODE ){
					var quote:Cotizacion = this.getQuote();
					Util.showProperties( quote );
					this.responseSave.token = this.cotizacionService.save( quote );
				}
				if( this.mode == Constants.UPDATE_MODE ){
					Alert.show("Confirme si desea actualizar el elemento.","Advertencia",Alert.YES+Alert.NO,this, updateItem);
				}
			}
			
			private function updateItem(event:CloseEvent):void{
				if( event.detail == Alert.NO ){
					return;
				}
				var quote:Cotizacion = this.getQuote();
				Util.showProperties( quote );
				this.responseUpdate.token = this.cotizacionService.update( quote );
			}
			
			private function validations():Boolean
			{
				var value:Boolean = true;
				value &&= this.validators.validarFormulario();
				value &&= this.dtfDate.selectedDate != null;
				value &&= this.dlvFileName.existElements();
				return value;
			}
			
			private function modeValidation():void
			{
				if( this.mode == Constants.REGISTER_MODE ){
					form.removeChild( this.frmID );
				}
			}
			
			private function initializeValidators():void
			{
				this.validators = new InicializaValidadoresEtiquetas();
				validators.crearValidadorRestrictor("generic",ValidadorCadenaCualquierCaracter.nombre, 0, 255);
				validators.setValidador(txtDescription, "generic");
				validators.setValidador(txtTransmitter, "generic");
			}
			
			protected function responseSave_resultHandler(event:ResultEvent):void
			{
				var eventQuoteNew:Event = new Event("quoteNew");
				this.dispatchEvent( eventQuoteNew );
				this.cleanForm();
			}
			
			private function cleanForm():void
			{
				Util.upScroll( this );
				this.txtID.reset();
				this.txtDescription.reset();
				this.txtTransmitter.reset();
				this.dtfDate.reset();
				this.dlvFileName.reset();
				this.comments.reset();
			}
			
			protected function responseUpdate_resultHandler(event:ResultEvent):void
			{
				var eventQuoteUpdated:Event = new Event("quoteUpdated");
				this.dispatchEvent( eventQuoteUpdated );
				this.cleanForm();
			}
			
			protected function catalogoService_faultHandler(event:FaultEvent):void
			{
				Util.showErrorMessage( Util.splitException( event.fault.faultString ) );
			}
			
		]]>
	</mx:Script>
	<mx:Metadata>
		[Event(name="quoteNew", type="flash.events.Event")]
		[Event(name="quoteUpdated", type="flash.events.Event")]
	</mx:Metadata>
	<mx:Form id="form">
		<mx:FormItem required="true" label="ID:" id="frmID">
			<components:TextInput id="txtID" textAlign="right" editable="false"/>
		</mx:FormItem>
		<mx:FormItem required="true" label="Descripción:">
			<components:TextInput id="txtDescription" width="400"/>
		</mx:FormItem>
		<mx:FormItem required="true" label="Emisor:">
			<components:TextInput id="txtTransmitter"
								  width="500"/>
		</mx:FormItem>
		<mx:FormItem required="true" label="Fecha:">
			<components:DateField id="dtfDate"/>
		</mx:FormItem>
		<mx:FormItem required="true" label="Nombre Archivo:">
			<components:FlatDeliver id="dlvFileName" limitedTo="5"/>
		</mx:FormItem>
		<components:Comments id="comments"/>
		<mx:FormItem>
			<mx:Button id="btnAccept" label="Aceptar" click="btnAccept_clickHandler(event)"/>
		</mx:FormItem>
	</mx:Form>
	<mx:RemoteObject id="cotizacionService" destination="cotizacionService" fault="cotizacionService_faultHandler(event)"
					 showBusyCursor="true"/>
	<mx:CallResponder id="responseSave" result="responseSave_resultHandler(event)"/>
	<mx:CallResponder id="responseUpdate" result="responseUpdate_resultHandler(event)"/>
</mx:VBox>
