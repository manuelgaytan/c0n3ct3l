<?xml version="1.0" encoding="ISO-8859-1"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" height="100%"
		 label="Consulta"
		 horizontalAlign="right"
		 creationComplete="creationCompleteHandler(event)" xmlns:components="components.*">
	<mx:Script>
		<![CDATA[
			import events.GenericEvent;
			
			import model.ProyectoPadre;
			import model.Util;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			
			[Bindable]
			private var items:ArrayCollection = null;
			
			private var proyectoPadre:ProyectoPadre;
			
			[Bindable]
			private var _consultOnly:Boolean = false;
			
			public function set consultOnly(value:Boolean):void{
				this._consultOnly = value;
			}
			
			public function selectedParentProyect():ProyectoPadre
			{
				var item:ProyectoPadre = this.datagrid.selectedItem as ProyectoPadre;
				return item;
			}
			
			public function setDataGridData(datas:ArrayCollection):void
			{
				items = datas;
			}
			
			protected function responseGetAll_resultHandler(event:ResultEvent):void
			{
				items = event.result as ArrayCollection;
			}
			
			protected function proyectoPadreService_faultHandler(event:FaultEvent):void
			{
				Util.showErrorMessage( Util.splitException( event.fault.faultString ) );
			}
			
			protected function creationCompleteHandler(event:FlexEvent):void
			{
				this.executeQuery();
			}
			
			protected function datagrid_doubleClickHandler(event:MouseEvent):void
			{
				var item:ProyectoPadre = this.datagrid.selectedItem as ProyectoPadre;
				if( item == null ){
					return;
				}
				this.responseGetItem.token = this.proyectoPadreService.getItem( item.id );
			}
			
			protected function responseGetItem_resultHandler(event:ResultEvent):void
			{
				var item:ProyectoPadre = event.result as ProyectoPadre;
				if( item == null ){
					return;
				}
				var genericEvent:GenericEvent = new GenericEvent("parentProjectDetail");
				genericEvent.information = item;
				this.dispatchEvent( genericEvent );
			}
			
			public function executeQuery():void
			{
				this.responseGetAll.token = this.proyectoPadreService.getAll();
			}
			
			protected function btnDelete_clickHandler(event:MouseEvent):void
			{
				if( this.datagrid.selectedItems == null ||
					this.datagrid.selectedItems.length == 0 ){
					Util.showErrorMessage("Debe seleccionar elementos de la tabla.");
					return;
				}
				Alert.show("Confirme si desea eliminar los elementos.","Advertencia",Alert.YES+Alert.NO,this, deleteItems);
			}
			
			private function deleteItems(event:CloseEvent):void
			{
				if( event.detail == Alert.NO ){
					return;
				}
				var items:ArrayCollection = Util.selectedIDs( this.datagrid.selectedItems ); 
				this.responseDeleteItems.token = this.proyectoPadreService.deleteItems( items );
			}
			
			protected function responseDeleteItems_resultHandler(event:ResultEvent):void
			{
				this.executeQuery();
			}
			
			private function labelFunctionGenerico(item:Object, column:DataGridColumn):String{
				return Util.extractToText( item, column.dataField );
			}
			
			private function labelFunctionProjectsAssociatedNumber(item:Object, column:DataGridColumn):String{
				var projectsAssociatedNumber:Number = 0;
				if( (item is ProyectoPadre) &&
					!((item as ProyectoPadre).proyectoPadreHijos == null) ){
					projectsAssociatedNumber = (item as ProyectoPadre).proyectoPadreHijos.length;
				}
				return projectsAssociatedNumber.toString();
			}
		]]>
	</mx:Script>
	<mx:Metadata>
		[Event(name="parentProjectDetail", type="events.GenericEvent")]
	</mx:Metadata>
	<mx:DataGrid id="datagrid" width="100%" height="100%" dataProvider="{items}"
				 doubleClickEnabled="true" doubleClick="datagrid_doubleClickHandler(event)"
				 allowMultipleSelection="true">
		<mx:columns>
			<mx:DataGridColumn id="dtgID" headerText="ID"
							   dataField="id"
							   width="80"
							   textAlign="right"/>
			<mx:DataGridColumn id="dtgDescription" headerText="{'Nombre de Proyecto'}"
							   width="360"
							   dataField="descripcion"
							   />
			<mx:DataGridColumn id="dtgDate" headerText="{'Fecha\nCreación'}"
							   width="100"
							   dataField="fechaCreacion"
							   labelFunction="{Util.labelFunctionDate}"
							   textAlign="center"/>
			<mx:DataGridColumn id="dtgProjectsAssociatedNumber" headerText="{'Número de\nProyectos \nAsociados'}"
							   width="100"
							   labelFunction="labelFunctionProjectsAssociatedNumber"
							   textAlign="center"
							   />
		</mx:columns>
	</mx:DataGrid>
	<mx:HBox>
		<mx:Button id="btnDelete" label="Eliminar" click="btnDelete_clickHandler(event)"
				   visible="{!this._consultOnly}"/>
		<components:ExportButton datagrid="{this.datagrid}"
								 visible="{!this._consultOnly}"/>
	</mx:HBox>
	<mx:RemoteObject id="proyectoPadreService" destination="proyectoPadreService" fault="proyectoPadreService_faultHandler(event)"
					 showBusyCursor="true"/>
	<mx:CallResponder id="responseGetAll" result="responseGetAll_resultHandler(event)"/>
	<mx:CallResponder id="responseGetItem" result="responseGetItem_resultHandler(event)"/>
	<mx:CallResponder id="responseDeleteItems" result="responseDeleteItems_resultHandler(event)"/>
</mx:VBox>
