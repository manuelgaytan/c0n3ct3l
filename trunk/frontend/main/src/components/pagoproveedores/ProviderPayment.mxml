<?xml version="1.0" encoding="ISO-8859-1"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:components="components.*"
		 creationComplete="creationCompleteHandler(event)" xmlns:proyectos="components.proyectos.*" xmlns:notascreditoclientes="components.notascreditoclientes.*" xmlns:informacionfacturas="components.informacionfacturas.*" xmlns:notascreditoproveedores="components.notascreditoproveedores.*" xmlns:facturacionproveedores="components.facturacionproveedores.*"
		 >
	<mx:Script>
		<![CDATA[
			import components.clientes.Client;
			
			import events.GenericEvent;
			
			import model.BancoConectel;
			import model.Catalogo;
			import model.Cliente;
			import model.Colaborador;
			import model.Constants;
			import model.Estado;
			import model.EstadoOrdenCompra;
			import model.EstadoValidacionCosto;
			import model.EstadoValidacionOperativa;
			import model.FacturaProveedor;
			import model.FiltroProyecto;
			import model.MedioTransporte;
			import model.NotaCreditoCliente;
			import model.NotaCreditoProveedor;
			import model.PagoProveedor;
			import model.Producto;
			import model.Proveedor;
			import model.ProveedorCalificado;
			import model.ProveedorMaquilador;
			import model.Proyecto;
			import model.TipoColaborador;
			import model.TipoEntregable;
			import model.TipoServicio;
			import model.Util;
			
			import mx.charts.chartClasses.DualStyleObject;
			import mx.collections.ArrayCollection;
			import mx.com.gahm.componentes.validadores.Constantes;
			import mx.com.gahm.componentes.validadores.InicializaValidadoresEtiquetas;
			import mx.com.gahm.componentes.validadores.ValidadorCadenaCualquierCaracter;
			import mx.com.gahm.componentes.validadores.ValidadorDecimal;
			import mx.com.gahm.componentes.validadores.ValidadorEntero;
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.events.ListEvent;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			
			private var mode:String = null;
			private var _item:PagoProveedor;
			private var validators:InicializaValidadoresEtiquetas;
			
			public function set registrer(value:Boolean):void{
				mode = Constants.REGISTER_MODE;
				this.label = Constants.REGISTRAR;
			}
			
			public function set update(value:Boolean):void{
				mode = Constants.UPDATE_MODE;
				this.label = Constants.MODIFICAR;
			}
			
			public function set enabledComponents(value:Boolean):void{
			}
			
			protected function creationCompleteHandler(event:FlexEvent):void
			{
				this.modeValidation();
				this.initializeValidators();
				this.fillCombos();
			}
			
			public function set providerPayment(value:PagoProveedor):void{
				if( value == null ){
					return;
				}
				this.cleanForm();
				this._item = value;
				this.setItem(); 
			}
			
			private function setItem():void
			{
				if( this._item == null ){
					return;
				}
				this.txtID.text = this._item.id.toString();
				if( !(this._item.facturaProveedor.proveedorCalificado == null) ){
					this.cmbSupplierType.selectedIndex = Util.selectedIndexFromCombo( this.cmbSupplierType.dataProvider as ArrayCollection, 1);
				}else{
					this.cmbSupplierType.selectedIndex = Util.selectedIndexFromCombo( this.cmbSupplierType.dataProvider as ArrayCollection, 2);
				}
				this.cmbSupplierType.dispatchEvent( new ListEvent(ListEvent.CHANGE) );
				this.txtInvoiceFolio.text = this._item.folioFactura;
				this.txtSubtotal.text = this._item.subtotal.toString();
				this.txtIVA.text = this._item.iva.toString();
				this.txtTotal.text = this._item.total.toString();
				if( Util.isFill( this.cmbConectelBank ) && this._item.bancoConectel != null ){
					this.cmbConectelBank.selectedIndex = Util.selectedIndexFromCombo( this.cmbConectelBank.dataProvider as ArrayCollection, 
						this._item.bancoConectel.id);
				}
				this.dtfPaymentDate.selectedDate = this._item.fechaAbono;
				this.txtAmount.text = this._item.monto.toString();
				this.txtPaymentReference.text = Util.numberToString( this._item.referenciaAbono, true);
				this.comments.setCommentsIn( this._item.comentariosPagoProveedor, "comentarioTesoreria");
			}
			
			private function getProviderPayment():PagoProveedor{
				var providerPayment:PagoProveedor = null;
				if( this.mode == Constants.REGISTER_MODE ){
					providerPayment = new PagoProveedor();
				}else{
					providerPayment = this._item;
				}
				providerPayment.facturaProveedor = this.vendorInvoiceConsult.datagrid.selectedItem as FacturaProveedor;
				providerPayment.folioFactura = Util.validateEmpty( this.txtInvoiceFolio.text );
				providerPayment.notaCreditoProveedor = this.creditNoteConsult.datagrid.selectedItem as NotaCreditoProveedor;
				providerPayment.subtotal = Util.stringToNumber( this.txtSubtotal.text );
				providerPayment.iva = Util.stringToNumber( this.txtIVA.text );
				providerPayment.total = Util.stringToNumber( this.txtTotal.text );
				providerPayment.bancoConectel = Util.validateCombo( this.cmbConectelBank ) as BancoConectel;
				providerPayment.fechaAbono = this.dtfPaymentDate.selectedDate;
				providerPayment.monto = Util.stringToNumber( this.txtAmount.text );
				providerPayment.referenciaAbono = Util.stringToNumber( this.txtPaymentReference.text );
				providerPayment.comentariosPagoProveedor = this.comments.getCommentsAsInWith("model.ComentarioTesoreria","model.ComentarioPagoProveedor","comentarioTesoreria");
				return providerPayment;
			}
			
			private function fillCombos():void
			{
				this.responseGetBancoConectel.token = this.bancoConectelService.getAll();
			}
			
			
			protected function pagoProveedorService_faultHandler(event:FaultEvent):void
			{
				Util.showErrorMessage( Util.splitException( event.fault.faultString ) );
			}
			
			protected function btnAccept_clickHandler(event:MouseEvent):void
			{
				if( !this.validations() ){
					Util.showMessageVerifyFields();
					return;
				}
				if( this.mode == Constants.REGISTER_MODE ){
					var providerPayment:PagoProveedor = this.getProviderPayment();
					Util.showProperties( providerPayment );
					this.responseSave.token = this.pagoProveedorService.save( providerPayment );
				}
				if( this.mode == Constants.UPDATE_MODE ){
					Alert.show("Confirme si desea actualizar el elemento.","Advertencia",Alert.YES+Alert.NO,this, updateItem);
				}
			}
			
			private function updateItem(event:CloseEvent):void{
				if( event.detail == Alert.NO ){
					return;
				}
				var item:PagoProveedor = this.getProviderPayment();
				Util.showProperties( item );
				this.responseUpdate.token = this.pagoProveedorService.update( item );
			}
			
			private function validations():Boolean
			{
				var value:Boolean = true;
				value &&= Util.listBaseSelected( this.cmbSupplier );
				value &&= this.validators.validarFormulario();
				value &&= this.vendorInvoiceConsult.datagrid.selectedItem != null;
				//value &&= this.creditNoteConsult.datagrid.selectedItem != null;
				value &&= Util.listBaseSelected( this.cmbConectelBank );
				return value;
			}
			
			private function modeValidation():void
			{
				if( this.mode == Constants.REGISTER_MODE ){
					form.removeChild( this.frmID );
				}
				if( this.mode == Constants.UPDATE_MODE ){
				}
			}
			
			private function initializeValidators():void
			{
				this.validators = new InicializaValidadoresEtiquetas();
				this.validators.crearValidadorRestrictor("genericAlfanumeric", ValidadorCadenaCualquierCaracter.nombre, 0, 255);
				this.validators.setRestrictor( this.txtInvoiceFolio, "genericAlfanumeric");
				this.validators.crearValidadorRestrictor("genericDecimal", ValidadorDecimal.nombre, 9, 2);
				this.validators.setRestrictor( this.txtSubtotal, "genericDecimal" );
				this.validators.setRestrictor( this.txtIVA, "genericDecimal" );
				this.validators.setRestrictor( this.txtTotal, "genericDecimal" );
				this.validators.setValidador( this.txtAmount, "genericDecimal" );
				this.validators.crearValidadorRestrictor("genericInteger", ValidadorEntero.nombre, 14);
				this.validators.setRestrictor( this.txtPaymentReference, "genericInteger" );
			}
			
			protected function responseSave_resultHandler(event:ResultEvent):void
			{
				var eventGeneralDataProjectNew:Event = new Event("providerPaymentNew");
				this.dispatchEvent( eventGeneralDataProjectNew );
				this.cleanForm();
			}
			
			private function cleanForm():void
			{
				Util.upScroll( this );
				this.txtID.reset();
				this.cmbSupplierType.selectedIndex = 0;
				this.cmbSupplierType.dispatchEvent( new ListEvent(ListEvent.CHANGE) );
				Util.removeItems(this.vendorInvoiceConsult.datagrid.dataProvider);
				this.txtInvoiceFolio.reset();
				Util.removeItems(this.creditNoteConsult.datagrid.dataProvider);
				this.txtSubtotal.reset();
				this.txtIVA.reset();
				this.txtTotal.reset();
				this.cmbConectelBank.selectedIndex = 0;
				this.dtfPaymentDate.reset();
				this.txtAmount.reset();
				this.txtPaymentReference.reset();
				this.comments.reset();
			}
			
			protected function responseUpdate_resultHandler(event:ResultEvent):void
			{
				var eventGeneralDataProjectUpdated:Event = new Event("providerPaymentUpdated");
				this.dispatchEvent( eventGeneralDataProjectUpdated );
				this.cleanForm();
			}
			
			protected function responseGetBancoConectel_resultHandler(event:ResultEvent):void
			{
				var data:ArrayCollection = event.result as ArrayCollection;
				if( data == null ){
					return;
				}
				this.cmbConectelBank.dataProvider = data;
				if( Util.isFill( this.cmbConectelBank ) && this._item 
					&& this._item.bancoConectel != null ){
					this.cmbConectelBank.selectedIndex = Util.selectedIndexFromCombo( this.cmbConectelBank.dataProvider as ArrayCollection, 
						this._item.bancoConectel.id);
				}
			}
			
			protected function vendorInvoiceConsult_vendorInvoiceDetailHandler(event:GenericEvent):void
			{
				var vendorInvoice:FacturaProveedor = this.vendorInvoiceConsult.datagrid.selectedItem as FacturaProveedor;
				if( vendorInvoice == null ){
					Util.showErrorMessage("Primero debe seleccionar un elemento.");
					return;
				}
				var subtotal:Number = 0;
				var iva:Number = 0;
				var total:Number = 0;
				if( vendorInvoice.ordenCompra != null ){
					subtotal = vendorInvoice.ordenCompra.subtotal;
					iva = vendorInvoice.ordenCompra.iva;
					total = vendorInvoice.ordenCompra.total;
				}else if(vendorInvoice.ordenCompraMaquilado.solicitudServicioMaquilado.proyecto.tipoServicio.id == TipoServicio.ID_LOCAL ){
					subtotal = Util.truncate(vendorInvoice.ordenCompraMaquilado.solicitudServicioMaquilado.proyecto.producto.costoLocalProveedor);
					iva = Util.truncate(vendorInvoice.ordenCompraMaquilado.solicitudServicioMaquilado.proyecto.producto.costoLocalProveedor * Constants.IVA);
					total = Util.truncate(subtotal + iva);
				}else if(vendorInvoice.ordenCompraMaquilado.solicitudServicioMaquilado.proyecto.tipoServicio.id == TipoServicio.ID_FORANEO ){
					subtotal = Util.truncate(vendorInvoice.ordenCompraMaquilado.solicitudServicioMaquilado.proyecto.producto.costoForaneoProveedor);
					iva = Util.truncate(vendorInvoice.ordenCompraMaquilado.solicitudServicioMaquilado.proyecto.producto.costoForaneoProveedor * Constants.IVA);
					total = Util.truncate(subtotal + iva);
				}
				this.setMontos( subtotal, iva, total);
			}
			
			protected function creditNoteConsult_creditNoteDetailHandler(event:GenericEvent):void
			{
				var creditNote:NotaCreditoProveedor = this.creditNoteConsult.datagrid.selectedItem as NotaCreditoProveedor;
				if( creditNote == null ){
					Util.showErrorMessage("Primero debe seleccionar un elemento.");
					return;
				}
				var subtotal:* = Util.stringToNumber( this.txtSubtotal.text );
				var iva:* = Util.stringToNumber( this.txtIVA.text );
				var total:* = Util.stringToNumber( this.txtTotal.text );
				subtotal = subtotal == undefined ? 0 : subtotal;
				iva = iva == undefined ? 0 : iva;
				total = total == undefined ? 0 : total;
				subtotal -= creditNote.subtotal;
				iva -= creditNote.iva;
				total -= creditNote.total;
				this.setMontos( subtotal, iva, total );
			}
			
			protected function responseCreditNoteGetAllByFiltro_resultHandler(event:ResultEvent):void
			{
				creditNoteConsult.setDataGridData( event.result as ArrayCollection );
				if( !(this._item == null) && !(this._item.notaCreditoProveedor == null) ){
					this.creditNoteConsult.datagrid.dataProvider = Util.asArrayCollection( this._item.notaCreditoProveedor );
					this.creditNoteConsult.datagrid.selectedIndex = 0;
				}
			}
			
			protected function responseGetAllByFiltro_resultHandler(event:ResultEvent):void
			{
				vendorInvoiceConsult.setDataGridData( event.result as ArrayCollection );
				if( !(this._item == null) && !(this._item.facturaProveedor == null) ){
					this.vendorInvoiceConsult.datagrid.dataProvider = Util.asArrayCollection( this._item.facturaProveedor );
					this.vendorInvoiceConsult.datagrid.selectedIndex = 0;
				}
			}
			
			protected function btnViewCreditNote_clickHandler(event:MouseEvent):void
			{
				var creditNote:NotaCreditoCliente = this.creditNoteConsult.datagrid.selectedItem as NotaCreditoCliente;
				if( creditNote == null ){
					Util.showErrorMessage("Primero debe seleccionar un elemento.");
					return;
				}
				var genericEvent:GenericEvent = new GenericEvent("creditNoteSelected");
				genericEvent.information = creditNote;
				this.dispatchEvent( genericEvent );
			}
			
			protected function btnViewVendorInvoice_clickHandler(event:MouseEvent):void
			{
				var vendorInvoice:FacturaProveedor = this.vendorInvoiceConsult.datagrid.selectedItem as FacturaProveedor;
				if( vendorInvoice == null ){
					Util.showErrorMessage("Primero debe seleccionar un elemento.");
					return;
				}
				var genericEvent:GenericEvent = new GenericEvent("vendorInvoiceSelected");
				genericEvent.information = vendorInvoice;
				this.dispatchEvent( genericEvent );
			}
			
			private function setMontos(subtotal:Number, iva:Number, total:Number):void{
				this.txtSubtotal.text = Util.numberToString(subtotal, true);
				this.txtIVA.text = Util.numberToString(iva, true);
				this.txtTotal.text = Util.numberToString(total, true);
				this.txtAmount.text = Util.numberToString(total, true); 
			}
			
			protected function responseGetProveedores_resultHandler(event:ResultEvent):void
			{
				var data:ArrayCollection = event.result as ArrayCollection;
				if( data == null ){
					return;
				}
				if( Util.selectedId(this.cmbSupplierType) == 1 ){ //Calificados
					this.cmbSupplier.dataProvider = Util.addEmptyCatalgo(data);
				}else{	//Maquiladores
					this.cmbSupplier.dataProvider = Util.addEmptyCatalgo(data,"nombreComercial");
				}
				if( Util.isFill( this.cmbSupplier ) && this._item 
					&& this._item.facturaProveedor != null 
					&& (this._item.facturaProveedor.proveedorCalificado != null || this._item.facturaProveedor.proveedorMaquilador) ){
					this.cmbSupplier.selectedIndex = Util.selectedIndexFromCombo( this.cmbSupplier.dataProvider as ArrayCollection, 
						(this._item.facturaProveedor.proveedorCalificado == null ? this._item.facturaProveedor.proveedorMaquilador.id : this._item.facturaProveedor.proveedorCalificado.id));
				}
				this.cmbSupplier.dispatchEvent( new ListEvent( ListEvent.CHANGE ) );
			}
			
			protected function cmbSupplier_changeHandler(event:ListEvent):void
			{
				if( !Util.listBaseSelected( this.cmbSupplier, "id", true ) ){
					return;
				}
				var idSupplier:Number = null; 
				if( Util.selectedId( this.cmbSupplierType ) == 1 ){
					var proveedorCalificado:ProveedorCalificado = Util.validateCombo( this.cmbSupplier ) as ProveedorCalificado;
					idSupplier = proveedorCalificado.id;
					this.responseGetAllByFiltro.token = this.facturaProveedorService.getAllByIdCalificado( idSupplier );
					this.responseCreditNoteGetAllByFiltro.token = this.notaCreditoProveedorService.getAllByIdCalificado( idSupplier );
				}else{
					var proveedorMaquilador:ProveedorMaquilador = Util.validateCombo( this.cmbSupplier ) as ProveedorMaquilador;
					idSupplier = proveedorMaquilador.id;
					this.responseGetAllByFiltro.token = this.facturaProveedorService.getAllByIdMaquilador( idSupplier );
					this.responseCreditNoteGetAllByFiltro.token = this.notaCreditoProveedorService.getAllByIdMaquilador( idSupplier );
				}
			}
			
				
			protected function cmbSupplierType_changeHandler(event:ListEvent):void
			{
				if( !Util.listBaseSelected( this.cmbSupplierType, "id", true ) ){
					return;
				}
				if( Util.selectedId(this.cmbSupplierType) == 1 ){ //Calificados
					this.cmbSupplier.labelField = null;
					this.cmbSupplier.labelFunction = qualifiedSupplier;
					this.responseGetAllProveedorCalificado.token = this.proveedorCalificadoService.getProveedoresCalificados();
				}else{	//Maquiladores
					this.cmbSupplier.labelField = null;
					this.cmbSupplier.labelFunction = maquiladorSupplier;
					this.responseGetAllProveedorMaquilador.token = this.proveedorMaquiladorService.getProveedoresMaquiladores();
				}
			}
			
			private function qualifiedSupplier( item:Object ):String{
				return Util.extractToText( item, "proveedorSeleccionado.proveedor.nombreComercial" );
			}
			
			private function maquiladorSupplier( item:Object ):String{
				return Util.extractToText( item, "proveedor.nombreComercial" );
			}
			
			private function getSupplierTypes():ArrayCollection{
				var datas:ArrayCollection = new ArrayCollection();
				var catalogo:Catalogo = null;
				catalogo = new Catalogo();
				catalogo.id = 1;
				catalogo.etiqueta = "Calificados";
				datas.addItem( catalogo );
				catalogo = new Catalogo();
				catalogo.id = 2;
				catalogo.etiqueta = "Maquiladores";
				datas.addItem( catalogo );
				return Util.addEmptyCatalgo( datas );
			}
		]]>
	</mx:Script>
	<mx:Metadata>
		[Event(name="providerPaymentNew", type="flash.events.Event")]
		[Event(name="providerPaymentUpdated", type="flash.events.Event")]
		[Event(name="providerPaymentProjectDone", type="flash.events.Event")]
		[Event(name="creditNoteSelected", type="events.GenericEvent")]
		[Event(name="vendorInvoiceSelected", type="events.GenericEvent")]
	</mx:Metadata>
	<mx:Form id="form">
		<mx:FormItem id="frmID" label="ID:" direction="horizontal">
			<components:TextInput id="txtID" width="80" textAlign="right"
								  editable="false"/>
		</mx:FormItem>
		<mx:FormItem label="Tipo de Proveedor:">
			<mx:ComboBox id="cmbSupplierType" dataProvider="{this.getSupplierTypes()}"
						 change="cmbSupplierType_changeHandler(event)"
						 labelField="etiqueta"
						 />
		</mx:FormItem>
		<mx:FormItem label="Proveedor:">
			<mx:ComboBox id="cmbSupplier"
						 change="cmbSupplier_changeHandler(event)"
						 />
		</mx:FormItem>
		<mx:FormItem label="Facturación Proveedores:">
			<facturacionproveedores:Consult id="vendorInvoiceConsult" itemDetail="vendorInvoiceConsult_vendorInvoiceDetailHandler(event)"
										 readOnly="true" noExecuteQuery="true"/>
			<mx:Button id="btnViewVendorInvoice" label="Ver Factura Proveedor" click="btnViewVendorInvoice_clickHandler(event)"/>
		</mx:FormItem>
		<mx:FormItem label="Folio Factura:">
			<components:TextInput id="txtInvoiceFolio" textAlign="right" width="100"/>
		</mx:FormItem>
		<mx:FormItem label="Nota Crédito Proveedor:">
			<notascreditoproveedores:Consult id="creditNoteConsult" creditNoteDetail="creditNoteConsult_creditNoteDetailHandler(event)"
										  readOnly="true" noExecuteQuery="true" 
										  width="650"/>
			<mx:Button id="btnViewCreditNote" label="Ver Nota Crédito Proveedor" click="btnViewCreditNote_clickHandler(event)"/>
		</mx:FormItem>
		<mx:FormItem label="Subtotal:">
			<components:TextInput id="txtSubtotal" textAlign="right" width="100"/>
		</mx:FormItem>
		<mx:FormItem label="I.V.A.:">
			<components:TextInput id="txtIVA" textAlign="right" width="100"/>
		</mx:FormItem>
		<mx:FormItem label="Total:">
			<components:TextInput id="txtTotal" textAlign="right" width="100"/>
		</mx:FormItem>
		<mx:FormItem label="Banco Conectel:">
			<mx:ComboBox id="cmbConectelBank" width="200"
						 labelField="banco"/>
		</mx:FormItem>
		<mx:FormItem label="Fecha:">
			<components:DateField id="dtfPaymentDate"
								  creationComplete="this.dtfPaymentDate.selectedDate = new Date()"/>
		</mx:FormItem>
		<mx:FormItem label="Monto:">
			<components:TextInput id="txtAmount" textAlign="right" width="100"/>
		</mx:FormItem>
		<mx:FormItem label="Referencia:">
			<components:TextInput id="txtPaymentReference" textAlign="right" width="160"/>
		</mx:FormItem>
		<components:Comments id="comments"/>
		<mx:FormItem direction="horizontal">
			<mx:Button id="btnAccept" label="Aceptar" click="btnAccept_clickHandler(event)"/>
		</mx:FormItem>
	</mx:Form>
	<mx:RemoteObject id="pagoProveedorService" destination="pagoProveedorService" fault="pagoProveedorService_faultHandler(event)"
					 showBusyCursor="true"/>
	<mx:CallResponder id="responseSave" result="responseSave_resultHandler(event)"/>
	<mx:CallResponder id="responseUpdate" result="responseUpdate_resultHandler(event)"/>
	<mx:RemoteObject id="bancoConectelService" destination="bancoConectelService" fault="pagoProveedorService_faultHandler(event)"
					 showBusyCursor="true"/>
	<mx:CallResponder id="responseGetBancoConectel" result="responseGetBancoConectel_resultHandler(event)"/>
	<mx:RemoteObject id="proveedorCalificadoService" destination="proveedorCalificadoService" fault="pagoProveedorService_faultHandler(event)"
					 showBusyCursor="true"/>
	<mx:CallResponder id="responseGetAllProveedorCalificado" result="responseGetProveedores_resultHandler(event)"/>
	<mx:RemoteObject id="proveedorMaquiladorService" destination="proveedorMaquiladorService" fault="pagoProveedorService_faultHandler(event)"
					 showBusyCursor="true"/>
	<mx:CallResponder id="responseGetAllProveedorMaquilador" result="responseGetProveedores_resultHandler(event)"/>
	<mx:RemoteObject id="facturaProveedorService" destination="facturaProveedorService" fault="pagoProveedorService_faultHandler(event)"
					 showBusyCursor="true"/>
	<mx:CallResponder id="responseGetAllByFiltro" result="responseGetAllByFiltro_resultHandler(event)"/>
	<mx:RemoteObject id="notaCreditoProveedorService" destination="notaCreditoProveedorService" fault="pagoProveedorService_faultHandler(event)"
					 showBusyCursor="true"/>
	<mx:CallResponder id="responseCreditNoteGetAllByFiltro" result="responseCreditNoteGetAllByFiltro_resultHandler(event)"/>
</mx:VBox>