<?xml version="1.0" encoding="ISO-8859-1"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:components="components.*"
		 creationComplete="creationCompleteHandler(event)"
		 >
	<mx:Script>
		<![CDATA[
			import model.Cliente;
			import model.Constants;
			import model.NotaCreditoCliente;
			import model.ProveedorCalificado;
			import model.ProveedorMaquilador;
			import model.Util;
			
			import mx.collections.ArrayCollection;
			import mx.com.gahm.componentes.validadores.InicializaValidadoresEtiquetas;
			import mx.com.gahm.componentes.validadores.ValidadorCadenaCualquierCaracter;
			import mx.com.gahm.componentes.validadores.ValidadorCadenaCualquierCaracter;
			import mx.com.gahm.componentes.validadores.ValidadorDecimal;
			import mx.com.gahm.componentes.validadores.ValidadorNumerico;
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			import mx.utils.object_proxy;
			
			private var mode:String = null;
			private var _item:NotaCreditoCliente;
			private var validators:InicializaValidadoresEtiquetas;
			
			[Bindable]
			private var _readOnly:Boolean = false;
			
			public function set readOnly(value:Boolean):void{
				this._readOnly = value;
				this.label = Constants.DETALLE;
			}
			
			public function set registrer(value:Boolean):void{
				mode = Constants.REGISTER_MODE;
				this.label = Constants.REGISTRAR;
			}
			
			public function set update(value:Boolean):void{
				mode = Constants.UPDATE_MODE;
				this.label = Constants.MODIFICAR;
			}
			
			public function set enabledComponents(value:Boolean):void{
			}
			
			protected function creationCompleteHandler(event:FlexEvent):void
			{
				this.modeValidation();
				this.initializeValidators();
				this.fillCombos();
			}
			
			public function set creditNote(value:NotaCreditoCliente):void{
				if( value == null ){
					return;
				}
				this.cleanForm();
				this._item = value;
				this.setCreditNote(); 
			}
			
			private function setCreditNote():void
			{
				if( this._item == null ){
					return;
				}
				this.txtID.text = String(this._item.id);
				this.txtFolio.text = this._item.folio;
				this.txtInvoice.text = this._item.factura;
				this.txtItem.text = this._item.item;
				this.dtfDate.selectedDate = this._item.fecha;
				this.txtSubtotal.text = this._item.subtotal.toString();
				this.txtIVA.text = this._item.iva.toString();
				this.txtTotal.text = this._item.total.toString();
				if( Util.isFill( this.cmbClient ) && this._item.cliente != null ){
					this.cmbClient.selectedIndex = Util.selectedIndexFromCombo( this.cmbClient.dataProvider as ArrayCollection, 
						this._item.cliente.id);
				}
				this.comments.setCommentsIn( this._item.comentariosNotaCreditoCliente, "comentarioCuentasPagarFacturacion");
			}
			
			private function getCreditNote():NotaCreditoCliente{
				var item:NotaCreditoCliente = null;
				if( this.mode == Constants.REGISTER_MODE ){
					item = new NotaCreditoCliente();
				}else{
					item = this._item;
				}
				item.folio = Util.validateEmpty( this.txtFolio.text );
				item.factura = Util.validateEmpty( this.txtInvoice.text );
				item.item = Util.validateEmpty( this.txtItem.text );
				item.fecha = this.dtfDate.selectedDate;
				item.subtotal = Util.stringToNumber( this.txtSubtotal.text );
				item.iva = Util.stringToNumber( this.txtIVA.text );
				item.total = Util.stringToNumber( this.txtTotal.text );
				item.cliente = Util.validateCombo( this.cmbClient ) as Cliente;
				item.comentariosNotaCreditoCliente = this.comments.getCommentsAsInWith("model.ComentarioCuentasPagarFacturacion","model.ComentarioNotaCreditoCliente","comentarioCuentasPagarFacturacion");
				return item;
			}
			
			private function fillCombos():void
			{
				this.responseGetAll.token = this.clienteService.getAll();
			}
			
			protected function notaCreditoClienteService_faultHandler(event:FaultEvent):void
			{
				Util.showErrorMessage( Util.splitException( event.fault.faultString ) );
			}
			
			protected function btnAccept_clickHandler(event:MouseEvent):void
			{
				if( !this.validations() ){
					Util.showMessageVerifyFields();
					return;
				}
				if( this.mode == Constants.REGISTER_MODE ){
					var creditNote:NotaCreditoCliente = this.getCreditNote();
					Util.showProperties( creditNote );
					this.responseSave.token = this.notaCreditoClienteService.save( creditNote );
				}
				if( this.mode == Constants.UPDATE_MODE ){
					Alert.show("Confirme si desea actualizar el elemento.","Advertencia",Alert.YES+Alert.NO,this, updateItem);
				}
			}
			
			private function updateItem(event:CloseEvent):void{
				if( event.detail == Alert.NO ){
					return;
				}
				var creditNote:NotaCreditoCliente = this.getCreditNote();
				Util.showProperties( creditNote );
				this.responseUpdate.token = this.notaCreditoClienteService.update( creditNote );
			}
			
			private function validations():Boolean
			{
				var value:Boolean = true;
				value &&= this.validators.validarFormulario();
				value &&= this.dtfDate.selectedDate != null;
				value &&= Util.listBaseSelected( this.cmbClient );
				return value;
			}
			
			private function modeValidation():void
			{
				if( this.mode == Constants.REGISTER_MODE ){
					form.removeChild( this.frmID );
				}
			}
			
			private function initializeValidators():void
			{
				this.validators = new InicializaValidadoresEtiquetas();
				validators.crearValidadorRestrictor("generic",ValidadorCadenaCualquierCaracter.nombre, 0, 255);
				validators.setValidador(txtFolio, "generic");
				validators.setValidador(txtInvoice, "generic");
				validators.setRestrictor(txtItem, "generic");
				validators.crearValidadorRestrictor("decimalGeneric",ValidadorDecimal.nombre, 7,2);
				validators.setValidador(txtSubtotal, "decimalGeneric");
				validators.setRestrictor(txtIVA, "decimalGeneric");
				validators.setValidador(txtTotal, "decimalGeneric");
			}
			
			protected function responseSave_resultHandler(event:ResultEvent):void
			{
				var eventCreditNoteNew:Event = new Event("creditNoteNew");
				this.dispatchEvent( eventCreditNoteNew );
				this.cleanForm();
			}
			
			private function cleanForm():void
			{
				Util.upScroll( this );
				this.txtID.reset();
				this.txtFolio.reset();
				this.txtInvoice.reset();
				this.txtItem.reset();
				this.dtfDate.reset();
				this.txtSubtotal.reset();
				this.txtIVA.reset();
				this.txtTotal.reset();
				this.cmbClient.selectedIndex = 0;
				this.comments.reset();
			}
			
			protected function responseUpdate_resultHandler(event:ResultEvent):void
			{
				var eventCreditNoteUpdated:Event = new Event("creditNoteUpdated");
				this.dispatchEvent( eventCreditNoteUpdated );
				this.cleanForm();
			}
			
			protected function catalogoService_faultHandler(event:FaultEvent):void
			{
				Util.showErrorMessage( Util.splitException( event.fault.faultString ) );
			}
			
			protected function responseGetAll_resultHandler(event:ResultEvent):void
			{
				var data:ArrayCollection = event.result as ArrayCollection;
				if( data == null ){
					return;
				}
				this.cmbClient.dataProvider = data;
				if( Util.isFill( this.cmbClient ) && this._item 
					&& this._item.cliente != null ){
					this.cmbClient.selectedIndex = Util.selectedIndexFromCombo( this.cmbClient.dataProvider as ArrayCollection, 
						this._item.cliente.id );
				}
			}
			
			private function qualifiedSupplier( item:Object ):String{
				return Util.extractToText( item, "proveedorSeleccionado.nombreComercial" );
			}
		]]>
	</mx:Script>
	<mx:Metadata>
		[Event(name="creditNoteNew", type="flash.events.Event")]
		[Event(name="creditNoteUpdated", type="flash.events.Event")]
	</mx:Metadata>
	<mx:Form id="form">
		<mx:FormItem label="ID:" id="frmID">
			<components:TextInput id="txtID" textAlign="right" editable="false"/>
		</mx:FormItem>
		<mx:FormItem label="Folio:">
			<components:TextInput id="txtFolio" textAlign="right"/>
		</mx:FormItem>
		<mx:FormItem label="Factura:">
			<components:TextInput id="txtInvoice" textAlign="right"/>
		</mx:FormItem>
		<mx:FormItem label="Ítem:">
			<components:TextInput id="txtItem" textAlign="right"/>
		</mx:FormItem>
		<mx:FormItem label="Fecha:">
			<components:DateField id="dtfDate"/>
		</mx:FormItem>
		<mx:FormItem label="Cliente:">
			<mx:ComboBox id="cmbClient"
						 labelField="nombreComercial"/>
		</mx:FormItem>
		<mx:FormItem label="Subtotal:">
			<components:TextInput id="txtSubtotal"
								  textAlign="right"
								  focusOut="Util.calculateIVA(txtSubtotal, txtIVA, txtTotal)"
								  enter="Util.calculateIVA(txtSubtotal, txtIVA, txtTotal)"/>
		</mx:FormItem>
		<mx:FormItem label="I.V.A.:">
			<components:TextInput id="txtIVA"
								  textAlign="right"/>
		</mx:FormItem>
		<mx:FormItem label="Total:">
			<components:TextInput id="txtTotal"
								  textAlign="right"/>
		</mx:FormItem>
		<components:Comments id="comments"/>
		<mx:FormItem>
			<mx:Button id="btnAccept" label="Aceptar" click="btnAccept_clickHandler(event)" visible="{!this._readOnly}"/>
		</mx:FormItem>
	</mx:Form>
	<mx:RemoteObject id="notaCreditoClienteService" destination="notaCreditoClienteService" fault="notaCreditoClienteService_faultHandler(event)"
					 showBusyCursor="true"/>
	<mx:CallResponder id="responseSave" result="responseSave_resultHandler(event)"/>
	<mx:CallResponder id="responseUpdate" result="responseUpdate_resultHandler(event)"/>
	<mx:RemoteObject id="clienteService" destination="clienteService" fault="notaCreditoClienteService_faultHandler(event)"
					 showBusyCursor="true"/>
	<mx:CallResponder id="responseGetAll" result="responseGetAll_resultHandler(event)"/>
</mx:VBox>
