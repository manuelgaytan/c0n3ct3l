<?xml version="1.0" encoding="ISO-8859-1"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:components="components.*"
		 creationComplete="creationCompleteHandler(event)" xmlns:proyectos="components.proyectos.*" xmlns:notascreditoclientes="components.notascreditoclientes.*" xmlns:informacionfacturas="components.informacionfacturas.*" xmlns:notascreditoproveedores="components.notascreditoproveedores.*" xmlns:facturacionproveedores="components.facturacionproveedores.*" xmlns:comprobacionesviaticos="components.comprobacionesviaticos.*" xmlns:seleccion="components.seleccion.*"
		 initialize="initializeHandler(event)" xmlns:reclutamientosolicitudempleo="components.reclutamientosolicitudempleo.*" xmlns:registroPercepcion="components.registroPercepcion.*" xmlns:contratacion="components.contratacion.*"
		 >
	<mx:Script>
		<![CDATA[
			import components.clientes.Client;
			
			import events.GenericEvent;
			
			import model.ClasificacionFalta;
			import model.ClasificacionPermiso;
			import model.Constants;
			import model.Contratacion;
			import model.Deduccion;
			import model.FormaPagoPrenomina;
			import model.Percepcion;
			import model.RegistroPercepcion;
			import model.TipoSancion;
			import model.Util;
			import model.Variacion;
			
			import mx.collections.ArrayCollection;
			import mx.com.gahm.componentes.validadores.Constantes;
			import mx.com.gahm.componentes.validadores.InicializaValidadoresEtiquetas;
			import mx.com.gahm.componentes.validadores.ValidadorCadenaCualquierCaracter;
			import mx.com.gahm.componentes.validadores.ValidadorDecimal;
			import mx.com.gahm.componentes.validadores.ValidadorEmail;
			import mx.com.gahm.componentes.validadores.ValidadorEntero;
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.events.ListEvent;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			
			[Bindable]
			private var mode:String = null;
			private var _item:RegistroPercepcion;
			private var validators:InicializaValidadoresEtiquetas;
			
			private var items:ArrayCollection = new ArrayCollection();
			
			public function set registrer(value:Boolean):void{
				mode = Constants.REGISTER_MODE;
				this.label = Constants.REGISTRAR;
			}
			
			public function set update(value:Boolean):void{
				mode = Constants.UPDATE_MODE;
				this.label = Constants.MODIFICAR;
			}
			
			public function set enabledComponents(value:Boolean):void{
			}
			
			protected function creationCompleteHandler(event:FlexEvent):void
			{
				this.modeValidation();
				this.initializeValidators();
				this.fillCombos();
			}
			
			public function set perception(value:RegistroPercepcion):void{
				if( value == null ){
					return;
				}
				this.cleanForm();
				this._item = value;
				this.setItem(); 
			}
			
			private function setItem():void
			{
				if( this._item == null ){
					return;
				}
				this.txtID.text = this._item.id.toString();
				this.recruitmentConsult.datagrid.dataProvider = Util.asArrayCollection( this._item.contratacion );
				this.recruitmentConsult.datagrid.selectedIndex = 0;
				if( Util.isFill( this.cmbPerception ) && this._item 
					&& this._item.percepcion != null ){
					this.cmbPerception.selectedIndex = Util.selectedIndexFromCombo( this.cmbPerception.dataProvider as ArrayCollection, 
						this._item.percepcion.id);
					this.cmbPerception.dispatchEvent( new ListEvent( ListEvent.CHANGE ) );
				}
				this.txtContributionAmount.text = Util.numberToString( this._item.montoAportacion, true);
				if( Util.isFill( this.cmbVariation ) && this._item 
					&& this._item.variacion != null ){
					this.cmbVariation.selectedIndex = Util.selectedIndexFromCombo( this.cmbVariation.dataProvider as ArrayCollection, 
						this._item.variacion.id);
				}
				this.txtCorrespondDays.text = Util.numberToString( this._item.diasCorrespondientes, true);
				this.dtfStartContractDate.selectedDate = this._item.contratacion.fechaInicioContrato;
				this.dtfInitialDate.selectedDate = this._item.fechaInicio;
				this.dtfFinalDate.selectedDate = this._item.fechaTermino;
				this.chkVacationPay.selected = this._item.primaVacacional;
			}
			
			private function getPerception():RegistroPercepcion{
				var perception:RegistroPercepcion = null;
				if( this.mode == Constants.REGISTER_MODE ){
					perception = new RegistroPercepcion();
				}else{
					perception = this._item;
				}
				perception.contratacion = this.recruitmentConsult.datagrid.selectedItem as Contratacion;
				perception.percepcion = Util.validateCombo( this.cmbPerception ) as Percepcion;
				perception.montoAportacion = Util.stringToNumber( this.txtContributionAmount.text );
				perception.variacion = Util.validateCombo( this.cmbVariation ) as Variacion;
				perception.diasCorrespondientes = Util.stringToNumber( this.txtCorrespondDays.text );
				perception.fechaInicio = this.dtfInitialDate.selectedDate;
				perception.fechaTermino = this.dtfFinalDate.selectedDate;
				perception.primaVacacional = this.chkVacationPay.selected;
				return perception;
			}
			
			private function fillCombos():void
			{
				this.responseGetPercepcion.token = this.catalogoService.getPercepcion();
				this.responseGetVariacion.token = this.catalogoService.getVariacion();
			}
			
			protected function registroPercepcionService_faultHandler(event:FaultEvent):void
			{
				Util.showErrorMessage( Util.splitException( event.fault.faultString ) );
			}
			
			protected function btnAccept_clickHandler(event:MouseEvent):void
			{
				if( this.mode == Constants.REGISTER_MODE ){
					if( this.items.length == 0 ){
						if( this.validations() ){
							this.btnAddItem.dispatchEvent( new MouseEvent( MouseEvent.CLICK ) );
							var timer:Timer = new Timer( 500, 1 );
							timer.addEventListener( TimerEvent.TIMER_COMPLETE, function():void{
								btnAccept.dispatchEvent( new MouseEvent( MouseEvent.CLICK ) );
								} );
							timer.start();
							return;
						}
						Util.showErrorMessage("Debe agregar elementos de la tabla.");
						return;
					}
					Util.showProperties( this.items );
					this.responseSave.token = this.registroPercepcionService.saveList( items );
					return;
				}
				if( !this.validations() ){
					Util.showMessageVerifyFields();
					return;
				}
				if( this.mode == Constants.UPDATE_MODE ){
					Alert.show("Confirme si desea actualizar el elemento.","Advertencia",Alert.YES+Alert.NO,this, updateItem);
				}
			}
			
			private function updateItem(event:CloseEvent):void{
				if( event.detail == Alert.NO ){
					return;
				}
				var item:RegistroPercepcion = this.getPerception();
				Util.showProperties( item );
				this.responseUpdate.token = this.registroPercepcionService.update( item );
			}
			
			private function validations():Boolean
			{
				var value:Boolean = true;
				value &&= this.validators.validarFormulario();
				value &&= recruitmentConsult.datagrid.selectedItem != null;
				value &&= Util.listBaseSelected( this.cmbPerception );
				return value;
			}
			
			private function modeValidation():void
			{
				if( this.mode == Constants.REGISTER_MODE ){
					form.removeChild( this.frmID );
				}
				if( this.mode == Constants.UPDATE_MODE ){
					form.removeChild( this.frmItmAddItem );
					form.removeChild( this.dtgPerceptions );
				}
			}
			
			private function initializeValidators():void
			{
				this.validators = new InicializaValidadoresEtiquetas();
				//validators.crearValidadorRestrictor("generic",ValidadorCadenaCualquierCaracter.nombre, 0, 255);
				this.validators.crearValidadorRestrictor("genericDecimal", ValidadorDecimal.nombre, 9, 2);
				this.validators.setRestrictor( this.txtContributionAmount, "genericDecimal" );
				validators.crearValidadorRestrictor("genericInteger",ValidadorEntero.nombre, 5);
				this.validators.setRestrictor( this.txtCorrespondDays, "genericInteger" );
			}
			
			protected function responseSave_resultHandler(event:ResultEvent):void
			{
				var eventGeneralDataProjectNew:Event = new Event("perceptionNew");
				this.dispatchEvent( eventGeneralDataProjectNew );
				this.cleanForm();
			}
			
			private function cleanForm(item:Boolean=false):void
			{
				if( !item ){
					Util.upScroll( this );
					this.txtID.reset();
					this.recruitmentConsult.datagrid.selectedItem = false;
					Util.removeItems( this.items );
				}
				this.cmbPerception.selectedIndex = 0;
				this.txtContributionAmount.reset();
				this.cmbVariation.selectedIndex = 0;
				this.txtCorrespondDays.reset();
				if( !item ){
					this.dtfStartContractDate.reset();
				}
				this.dtfInitialDate.reset();
				this.dtfFinalDate.reset();
				this.chkVacationPay.selected = false;
			}
			
			protected function responseUpdate_resultHandler(event:ResultEvent):void
			{
				var eventGeneralDataProjectUpdated:Event = new Event("perceptionUpdated");
				this.dispatchEvent( eventGeneralDataProjectUpdated );
				this.cleanForm();
			}
			
			protected function responseGetPercepcion_resultHandler(event:ResultEvent):void
			{
				var data:ArrayCollection = event.result as ArrayCollection;
				if( data == null ){
					return;
				}
				this.cmbPerception.dataProvider = data;
				if( Util.isFill( this.cmbPerception ) && this._item 
					&& this._item.percepcion != null ){
					this.cmbPerception.selectedIndex = Util.selectedIndexFromCombo( this.cmbPerception.dataProvider as ArrayCollection, 
						this._item.percepcion.id);
				}
				this.cmbPerception.dispatchEvent( new ListEvent( ListEvent.CHANGE ) );
			}
			
			protected function responseGetVariacion_resultHandler(event:ResultEvent):void
			{
				var data:ArrayCollection = event.result as ArrayCollection;
				if( data == null ){
					return;
				}
				this.cmbVariation.dataProvider = Util.addEmptyCatalgo( data, "tipo" );
				if( Util.isFill( this.cmbVariation ) && this._item 
					&& this._item.variacion != null ){
					this.cmbVariation.selectedIndex = Util.selectedIndexFromCombo( this.cmbVariation.dataProvider as ArrayCollection, 
						this._item.variacion.id);
				}
			}
			
			protected function initializeHandler(event:FlexEvent):void
			{
				if( this.mode == Constants.REGISTER_MODE ){
				}
				if( this.mode == Constants.UPDATE_MODE ){
				}
			}
			
			protected function recruitmentConsult_itemDetailHandler(event:GenericEvent):void
			{
				if( event == null || event.information == null ){
					return;
				}
				var item:Contratacion = event.information as Contratacion;
				this.dtfStartContractDate.selectedDate = item.fechaInicioContrato;
			}
						
			protected function cmbPerception_changeHandler(event:ListEvent):void
			{
				var isVacation:Boolean = this.validateIsVacation();
				if( isVacation ){
				}else{
					this.cmbVariation.selectedIndex = 0;
					this.txtCorrespondDays.text = "";
					this.dtfInitialDate.selectedDate = null;
					this.dtfFinalDate.selectedDate = null;
					this.chkVacationPay.selected = false;
				}
				this.cmbVariation.enabled = isVacation;
				this.txtCorrespondDays.enabled = isVacation;
				this.dtfInitialDate.enabled = isVacation;
				this.dtfFinalDate.enabled = isVacation;
				this.chkVacationPay.enabled = isVacation;
			}
			
			private function validateIsVacation():Boolean{
				var perception:Percepcion = this.cmbPerception.selectedItem as Percepcion;
				if( perception == null ){
					return false;
				}
				return (perception.id == Percepcion.ID_VACACIONES);
			}
			
			protected function btnAddItem_clickHandler(event:MouseEvent):void
			{
				if( !this.validations() ){
					Util.showMessageVerifyFields();
					return;
				}
				if( this.mode == Constants.REGISTER_MODE ){
					var perception:RegistroPercepcion = this.getPerception();
					Util.showProperties( perception );
					this.items.addItem( perception );
					this.cleanForm( true );
				}
			}
			
		]]>
	</mx:Script>
	<mx:Metadata>
		[Event(name="perceptionNew", type="flash.events.Event")]
		[Event(name="perceptionUpdated", type="flash.events.Event")]
		[Event(name="perceptionProjectDone", type="flash.events.Event")]
	</mx:Metadata>
	<mx:Form id="form">
		<mx:FormItem required="true" id="frmID" label="ID:" direction="horizontal">
			<components:TextInput id="txtID" width="80" textAlign="right"
								  editable="false"/>
		</mx:FormItem>
		<mx:FormItem required="true" label="Contratación:"/>
		<mx:VBox width="900">
			<mx:VBox width="100%" height="100%" verticalGap="5">
				<contratacion:Consult id="recruitmentConsult" readMode="true" itemDetail="recruitmentConsult_itemDetailHandler(event)"/>
			</mx:VBox>
		</mx:VBox>
		<mx:FormItem required="true" label="Percepción:">
			<mx:ComboBox id="cmbPerception" width="200"
						 labelField="percepcion" change="cmbPerception_changeHandler(event)"/>
		</mx:FormItem>
		<mx:FormItem label="Monto Aportación:">
			<components:TextInput id="txtContributionAmount" width="150"
								  textAlign="right"/>
		</mx:FormItem>
		<mx:FormItem label="Vacaciones:">
			<mx:ComboBox id="cmbVariation" width="200"
						 labelField="tipo"/>
		</mx:FormItem>
		<mx:FormItem label="Días Correspondientes:">
			<components:TextInput id="txtCorrespondDays" width="100" textAlign="right"/>
		</mx:FormItem>
		<mx:FormItem label="Fecha Inicio Contrato:">
			<components:DateField id="dtfStartContractDate" enabled="false"/>
		</mx:FormItem>
		<mx:FormItem label="Inicio:">
			<components:DateField id="dtfInitialDate"/>
		</mx:FormItem>
		<mx:FormItem label="Fin:">
			<components:DateField id="dtfFinalDate"/>
		</mx:FormItem>
		<mx:FormItem required="true" label="Prima Vacacional:">
			<mx:CheckBox id="chkVacationPay"/>
		</mx:FormItem>
		<mx:FormItem id="frmItmAddItem">
			<mx:Button id="btnAddItem" label="Agregar Percepción" click="btnAddItem_clickHandler(event)"/>
		</mx:FormItem>
		<mx:VBox width="900" id="dtgPerceptions">
			<mx:VBox width="100%" height="100%" verticalGap="5">
				<mx:DataGrid id="datagrid" width="900" height="100%" dataProvider="{items}"
							 doubleClickEnabled="true"
							 allowMultipleSelection="true"
							 >
					<mx:columns>
						<mx:DataGridColumn id="dtgPerception" headerText="{'Percepción'}"
										   dataField="percepcion.percepcion"
										   width="150"/>
						<mx:DataGridColumn id="dtgContributionAmount" headerText="{'Monto\nAportación'}"
										   dataField="montoAportacion"
										   textAlign="right"
										   width="80"/>
						<mx:DataGridColumn id="dtgVariation" headerText="{'Vacaciones'}"
										   dataField="variacion.tipo"
										   width="170"
										   />
						<mx:DataGridColumn id="dtgCorrespondDays" headerText="{'Días\nCorrespondientes'}"
										   dataField="diasCorrespondientes"
										   textAlign="right"
										   width="120"
										   />
						<mx:DataGridColumn id="dtgInitialDate" headerText="{'Fecha\nInicio\nContrato'}"
										   dataField="contratacion.fechaInicioContrato"
										   labelFunction="{Util.labelFunctionDate}"
										   textAlign="center"
										   width="100"
										   />
						<mx:DataGridColumn headerText="{'Fecha\nInicio'}"
										   dataField="fechaInicio"
										   labelFunction="{Util.labelFunctionDate}"
										   textAlign="center"
										   width="100"
										   />
						<mx:DataGridColumn id="dtgFinalDate" headerText="{'Fecha\nFin'}"
										   dataField="fechaTermino"
										   labelFunction="{Util.labelFunctionDate}"
										   textAlign="center"
										   width="100"
										   />
						<mx:DataGridColumn id="dtgInmediatlyAction" headerText="{'Prima\nVacacional'}" width="80" editable="false" textAlign="center"
										   dataField="primaVacacional">
							<mx:itemRenderer>
								<mx:Component>
									<mx:CheckBox selected="{data.primaVacacional}" enabled="false"/>
								</mx:Component>
							</mx:itemRenderer>
						</mx:DataGridColumn>
					</mx:columns>
				</mx:DataGrid>
			</mx:VBox>			
		</mx:VBox>
		<mx:FormItem direction="horizontal">
			<mx:Button id="btnAccept" label="Aceptar" click="btnAccept_clickHandler(event)"/>
		</mx:FormItem>
	</mx:Form>
	<mx:RemoteObject id="registroPercepcionService" destination="registroPercepcionService" fault="registroPercepcionService_faultHandler(event)"
					 showBusyCursor="true"/>
	<mx:CallResponder id="responseSave" result="responseSave_resultHandler(event)"/>
	<mx:CallResponder id="responseUpdate" result="responseUpdate_resultHandler(event)"/>
	<mx:RemoteObject id="catalogoService" destination="catalogoService" fault="registroPercepcionService_faultHandler(event)"
					 showBusyCursor="true"/>
	<mx:CallResponder id="responseGetPercepcion" result="responseGetPercepcion_resultHandler(event)"/>
	<mx:CallResponder id="responseGetVariacion" result="responseGetVariacion_resultHandler(event)"/>
</mx:VBox>